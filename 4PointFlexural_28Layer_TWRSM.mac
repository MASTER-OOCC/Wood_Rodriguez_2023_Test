! Script 
/CLEAR,
FINISH,

E_L   = 6430

F1t = 63.001   ! Tensile strength - Longitudinal
F2t = 6.1499   ! Tensile strength - Tangencial
F3t = 2.4199   ! Tensile strength - Radial
!!

! Compressive strengths 

F1c = -37.204 ! Compressive strength - Longitudinal
F2c = -5.0357  ! Compressive strength - Tangencial
F3c = -3.5717  ! Compressive strength - Radial
!!

!Shear strength

F4s = 13.131  ! Shear strength radial       - YZ
F5s = 12.192  ! Shear strength longitudinal - XZ
F6s = 14.419 ! Shear strength tangencial   - XY
!!

!# Parameters
Length = 3000
Length_int = 2400
Width  = 43
Height = 28!.75
LSpan  = 150
DistLoad = 1050
tol    = 1e-5
pulg   = 25.4       ! Inch value in millimeters (mm)
!# Material props / Material model
Cyldiameter = 50

/PREP7
! Element type
ET, 1,SOLID186
! Youngâ€™s moduli
MP, EX, 1,E_L 
MP, PRXY, 1,0.48! 0.430

NL=10 ! numero de laminas
*DO,i,1,NL,1
    BLOCK,0,Length,Height*(i-1),Height*(i),0,Width
*ENDDO

! Corte en apoyos
WPROTA,,,90
WPOFFS,,,LSpan - 40
VSBW,ALL
WPOFFS,,,80
VSBW,ALL
WPOFFS,,,2700
VSBW,ALL
WPOFFS,,,-80
VSBW,ALL

! Corte en cargas
WPSTYL,DEFA
WPROTA,,,90
WPOFFS,,,DistLoad - 40
VSBW,ALL
WPOFFS,,,80
VSBW,ALL
WPOFFS,,,900
VSBW,ALL
WPOFFS,,,-80
VSBW,ALL

! Corte en el medio
WPSTYL,DEFA
WPROTA,,,90
WPOFFS,,,Length/2 - 100
VSBW,ALL
WPOFFS,,,200
VSBW,ALL
VGLUE,ALL
WPSTYL,DEFA

NUMCMP,ALL

CM,WoodBeam,VOLU

ASEL,S,LOC,X,LSpan - 40,LSpan + 40
ASEL,A,LOC,X,Length - LSpan - 40, Length - LSpan + 40
ASEL,R,LOC,Y,0.0
CM,WoodBeamContactLower,AREA
ALLSEL,ALL

ASEL,S,LOC,X,DistLoad - 40,DistLoad + 40
ASEL,A,LOC,X,Length - DistLoad - 40, Length - DistLoad + 40
ASEL,R,LOC,Y,280.0!NL*Height
CM,WoodBeamContactUpper,AREA
ALLSEL,ALL

! MALLA CENTRAL
MSHAPE,0,3D     ! Mesh Type, Quadrilateral ,3 Dimensions
MSHKEY,1        ! Using the Mapped Meshing
ALLSEL,ALL
VATT,1,,1
! Zone 1
VSEL,U,LOC,X,LSpan - 40,Length - LSpan + 40
ESIZE,30
VMESH,ALL
ALLSEL,ALL
! Zone 2
VSEL,U,LOC,X,LSpan + 40,Length - LSpan - 40
ESIZE,15
VMESH,ALL
ALLSEL,ALL
! Zone 3
VSEL,U,LOC,X,DistLoad - 40,Length - DistLoad + 40
ESIZE,30
VMESH,ALL
ALLSEL,ALL
! Zone 4
VSEL,U,LOC,X,DistLoad + 40,Length - DistLoad - 40
ESIZE,15
VMESH,ALL
ALLSEL,ALL
! Zone 5
VSEL,U,LOC,X,Length/2 - 100,Length/2 + 100
ESIZE,30
VMESH,ALL
ALLSEL,ALL
! Zone 6
VSEL,S,LOC,X,Length/2 - 100,Length/2 + 100
ESIZE,30
VMESH,ALL
ALLSEL,ALL
! NODO AUXILIAR
*GET,nNodes,NODE,0,COUNT

!# Contact definitions
! Lower contact
ET,100,170
ET,110,174
R,110,Cyldiameter
!KEYOPT,110,5,3
! Target Element
TYPE,100
REAL,110
TSHAP,CYLINDER
! Left Rigid Target
N,nNodes+1,150 ,-Cyldiameter - tol,0.0-10
N,nNodes+2,150 ,-Cyldiameter - tol,Width+10
N,nNodes+3,2850,-Cyldiameter - tol,0.0-10
N,nNodes+4,2850,-Cyldiameter - tol,Width+10
!
E,nNodes+1,nNodes+2
E,nNodes+3,nNodes+4
! Right rigid target
! Contact Element
TYPE,110
ASEL,S,AREA,,WoodBeamContactLower
NSLA,S,1
ESLN,S
ESURF
ALLSEL,ALL

!# Contact definitions
! Upper contact
ET,200,170
ET,210,174
R,210,Cyldiameter
!KEYOPT,210,5,3
! Target Element
TYPE,200
REAL,210
TSHAP,CYLINDER
! Left Rigid Target
N,nNodes+5,1050,280.0 + Cyldiameter + tol,0.0-10
N,nNodes+6,1050,280.0 + Cyldiameter + tol,Width+10
N,nNodes+7,1950,280.0 + Cyldiameter + tol,0.0-10
N,nNodes+8,1950,280.0 + Cyldiameter + tol,Width+10

E,nNodes+5,nNodes+6
E,nNodes+7,nNodes+8
! Right rigid target
! Contact Element
TYPE,210
ASEL,S,AREA,,WoodBeamContactUpper
NSLA,S,1
ESLN,S
ESURF
ALLSEL,ALL

!! CONDICIONES DE BORDE
!# Constrain definition
!## Bottom Supports
NSEL,S,LOC,Y,-Cyldiameter - tol
D,ALL,ALL,0.0
ALLSEL,ALL
!!
!## Top Supports
NSEL,S,LOC,Y,280 + Cyldiameter + tol
CP,4,UX,ALL
CP,5,UY,ALL
CP,6,UZ,ALL
*GET,NodeLoad,NODE,0,NUM,MIN
ALLSEL,ALL


Displacement = 25
*DIM,Disp,TABLE,1,6,,,TIME
*taxis,Disp(1),2, 0, 0.1, 1.0
Disp(1,1) = -Displacement*0.0
Disp(1,2) = -Displacement*0.90
Disp(1,3) = -Displacement*0.92
Disp(1,4) = -Displacement*0.94
Disp(1,5) = -Displacement*0.98
Disp(1,6) = -Displacement*1

D,NodeLoad,UX,0.0
D,NodeLoad,UY,%Disp%
D,NodeLoad,UZ,0.0

! Restrain 1 node
NSEL,S,LOC,X,0.0
NSEL,R,LOC,Y,0.0
NSEL,R,LOC,Z,0.0
D,ALL,UX,0.0
ALLSEL,ALL

NSEL,S,LOC,Z,0.0
D,ALL,UZ,0.0
ALLSEL,ALL
FINISH,

dTime = 0.1
PASO = 20
/SOLU
ANTYPE,STATIC
NLGEOM,ON
NROPT,FULL
OUTRES,ALL,ALL
NSUBST,PASO,PASO,PASO
TIME,dTime
RESCONTROL,DEFINE,LAST,LAST,1,,1!RESCONTROL,,ALL,1
SOLVE,

nIter = 1/dTime-1

/POST1
RSYS,SOLU ! Activate the ESYS
!Stress in tensile
FC,1,S,XTEN,F1t
FC,1,S,YTEN,F2t
FC,1,S,ZTEN,F3t
!Stress in Compresion
FC,1,S,XCMP,F1c
FC,1,S,YCMP,F2c
FC,1,S,ZCMP,F3c
!Stress in shear
FC,1,S,XY,F6s
FC,1,S,YZ,F4s
FC,1,S,XZ,F5s
FC,1,S,XYCP,-1 ! Coefficient. Defaults to -1.0
FC,1,S,YZCP,-1 ! Coefficient. Defaults to -1.0
FC,1,S,XZCP,-1 ! Coefficient. Defaults to -1.0
FCTYP,ADD,HFIB,HMAT,PFIB,PMAT
ESEL,S,TYPE,,1
PRNSOL,S,FAIL       ! Print table with FAIL index, where:
PRESOL,FAIL
PLESOL,FAIL,TWSR    ! Print table with FAIL index, where:
PLESOL,FCMX,FC,TWSR ! Print table with FAIL index, where:

/POST1
SET,LAST

ESEL,S,TYPE,,1
ETABLE,check,FAIL,TWSR
ESEL,S,ETAB,check,1

CM,Kill_Loop,ELEM
*GET,Ekill_C,ELEM,0,COUNT
*IF,Ekill_C,GT,0,THEN
    CMWRITE,Kill_Loop,cm
*ENDIF
ALLSEL,ALL
FINISH,

*DO,k,2,nIter,
    /SOLU
   PARSAV,ALL,prepar
   ANTYPE,,RESTART
   PARRES,,prepar
   NSUBST,PASO,PASO,PASO
   TIME,k*dTime

    *IF,Ekill_C,GT,0,THEN
        /INPUT,Kill_Loop,cm
        CMSEL,S,Kill_Loop,ELEM
        EKILL,ALL
    *ENDIF
    ALLSEL,ALL
    SOLVE,
    FINISH,

    /POST1
    RSYS,SOLU ! Activate the ESYS
    !Stress in tensile
    FC,1,S,XTEN,F2t
    FC,1,S,YTEN,F3t
    FC,1,S,ZTEN,F1t
    !Stress in Compresion
    FC,1,S,XCMP,F2c
    FC,1,S,YCMP,F3c
    FC,1,S,ZCMP,F1c
    !Stress in shear
    FC,1,S,XY,F6s
    FC,1,S,YZ,F4s
    FC,1,S,XZ,F5s
    FC,1,S,XYCP,-1 ! Coefficient. Defaults to -1.0
    FC,1,S,YZCP,-1 ! Coefficient. Defaults to -1.0
    FC,1,S,XZCP,-1 ! Coefficient. Defaults to -1.0
    FCTYP,ADD,HFIB,HMAT,PFIB,PMAT
    ALLSEL,ALL
    ESEL,S,TYPE,,1
    PRNSOL,S,FAIL       ! Print table with FAIL index, where:
    PRESOL,FAIL
    PLESOL,FAIL,TWSR    ! Print table with FAIL index, where:
    PLESOL,FCMX,FC,TWSR ! Print table with FAIL index, where:

    SET,LAST
    ETABLE,check,FAIL,TWSR
    ESEL,S,ETAB,check,1

    *IF,Ekill_C,GT,0,THEN
        CMSEL,A,Kill_Loop,ELEM
    *ENDIF
    CM,Kill_Loop,ELEM
    *GET,Ekill_C,ELEM,0,COUNT
    *IF,Ekill_C,GT,0,THEN
        CMWRITE,Kill_Loop,cm
    *ENDIF
    ALLSEL,ALL
    FINISH,
*ENDDO
FINISH,

! Force-Displacement data
/POST1
*GET,nSubstep,ACTIVE,0,SET,NSET
*IF,nSubstep,EQ,999999,THEN
	SET,PREVIOUS
	*GET,nSubstep,ACTIVE,0,SET,NSET
*ENDIF
FINISH,

/POST26
NSOL,2,NodeLoad,U,Y
RFORCE,3,NodeLoad,F,Y
LINE,1000

*DIM,Results,TABLE,nSubstep,2
VGET,Results(1,1),2
VGET,Results(1,2),3
FINISH,

/OUTPUT,VIGA28_TWRSM,dat
*VWRITE,Results(1,1),Results(1,2)
(F14.8,F20.6)
/OUTPUT
